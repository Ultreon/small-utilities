plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.0-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

tasks.register('collectJars') {
    dependsOn ":forge:build"
    dependsOn ":fabric:build"
    dependsOn ":common:build"

    doFirst {
        delete fileTree("$buildDir/libs/collected")
        mkdir "$buildDir/libs/collected"
        copy {
            from "$projectDir/common/build/libs/$archivesBaseName-${version}.jar"
            into "$buildDir/libs/collected"
            rename ".*", "$archivesBaseName-common-${version}.jar"
        }
        copy {
            from "$projectDir/forge/build/libs/$archivesBaseName-${version}.jar"
            into "$buildDir/libs/collected"
            rename ".*", "$archivesBaseName-forge-${version}.jar"
        }
        copy {
            from "$projectDir/fabric/build/libs/$archivesBaseName-${version}.jar"
            into "$buildDir/libs/collected"
            rename ".*", "$archivesBaseName-fabric-${version}.jar"
        }
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.18.2+build.4:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.

        maven {
            name "Fuzs Mod Resources"
            url "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
        }
        maven {
            // Location of the maven that hosts Mine and Team Resourceful's files.
            name "Resourceful Bees Maven"
            url "https://nexus.resourcefulbees.com/repository/maven-public/"
        }
    }


    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
